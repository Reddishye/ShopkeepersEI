plugins {
    id 'java'
    id("com.gradleup.shadow") version "9.0.0-beta15"
    id("com.diffplug.spotless") version "7.2.1"
}

ext.getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
        ignoreExitValue = true // Ignore error if there is no Git commit yet
    }
    return stdout.toString().trim() ?: "0"
}

// We embed the git hash into jar files, and also use it for the plugin version of snapshot builds.
ext.buildVersion = version + '+' + getGitHash()
ext.isSnapshot = version.contains('-SNAPSHOT')
ext.pluginVersion = isSnapshot ? buildVersion : version

println 'Project version: ' + version
println 'Build version: ' + buildVersion
println 'Plugin version: ' + pluginVersion

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        url = uri("https://raw.githubusercontent.com/Shopkeepers/Repository/main/releases/")
    }

    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT" // spigot api

    annotationProcessor 'org.projectlombok:lombok:1.18.42' // lombok anotation processor (used only in configurate-yaml)

    implementation("de.exlll:configlib-paper:4.7.0") // Config
    implementation "com.google.inject:guice:7.0.0" // dependency injection
    implementation "net.kyori:adventure-text-serializer-plain:4.26.1" // text serializer
    implementation "net.kyori:adventure-text-serializer-legacy:4.26.1" // text serializer
    implementation "net.kyori:adventure-platform-bukkit:4.3.2" // adventure platform
    implementation "net.kyori:adventure-text-minimessage:4.26.1" // minimessage
    implementation "net.kyori:adventure-api:4.26.1" // adventure api
    implementation "org.slf4j:slf4j-api:2.0.0-alpha1" // logging

    compileOnly 'com.nisovin.shopkeepers:ShopkeepersAPI:2.25.0'


    compileOnly "org.projectlombok:lombok:1.18.42" // lombok

    implementation 'com.github.Ssomar-Developement:SCore:5.25.3.9'
    // to add: ExecutableBlocks, ExecutableEvents, Oraxen, MMOItems

    implementation 'com.github.Reddishye.Lamp:lamp.common:6a2f380f00'
    implementation 'com.github.Reddishye.Lamp:lamp.bukkit:6a2f380f00'
    implementation 'com.github.Reddishye.Lamp:lamp.brigadier:6a2f380f00'
}


def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

shadowJar {
    version = version
    archiveClassifier.set('') // remove the classifier
}

processResources {
    def props = [
        'pluginVersion': pluginVersion,
        'pluginUrl': pluginUrl,
        'pluginDescription': pluginDescription
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

spotless {
    //ratchetFrom("origin/main") // Uncomment in case of working in a feature branch (makes changes only to files on that branch)

    format("misc") {
        target(
                "*.gradle.kts",
                ".gitattributes",
                ".gitignore"
        )

        trimTrailingWhitespace()
        endWithNewline()
    }

    java {
        googleJavaFormat("1.26.0").aosp().reflowLongStrings().skipJavadocFormatting()
        formatAnnotations()
        removeUnusedImports()
    }
}